//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт

	ЮТТесты
		.ДобавитьТестовыйНабор("Конструктор текста")
			.ДобавитьТест("ДобавлениеНовойСтроки", "Конструтор текста. Добавление новой строки")
			.ДобавитьТест("ДобавлениеНовойСтрокиПоШаблону", "Конструтор текста. Добавление новой строки по шаблону")
				.СПараметрами("Сумма", "XMLЗначение(Тип(""Число""), ""1000"")").Представление("Конструтор текста. Добавление новой строки по шаблону. П1 = Сумма, П2 = 1000")
				.СПараметрами("Дата", "XMLЗначение(Тип(""Дата""), ""2025-05-30T09:00:00"")").Представление("Конструтор текста. Добавление новой строки по шаблону. П1 = Дата, П2 = XMLЗначение(Тип(""Дата""), ""2025-05-30T09:00:00"")")
			.ДобавитьТест("ДобавлениеТекста", "Конструтор текста. Добавить текст")
			.ДобавитьТест("ВставитьПервуюСтроку", "Конструтор текста. Вставить первую строку")
			.ДобавитьТест("ВставитьСтроку", "Конструтор текста. Вставить строку")
			.ДобавитьТест("ПереносСтроки", "Конструтор текста. Перенос строки")
			.ДобавитьТест("Табуляция", "Конструтор текста. Табуляция строки")
			.ДобавитьТест("Текст", "Конструтор текста. Получить готовый текст")
			.ДобавитьТест("Разделитель", "Конструтор текста. Заменить стандартный разделитель строк")
			.ДобавитьТест("Количество", "Конструтор текста. Количество строк добавленных")
			.ДобавитьТест("УстановитьТекстИсходныйТекстСоответствует", "Конструтор текста. Установить текст - исходный текст совпадает с результатом")
			.ДобавитьТест("УстановитьТекстИсходныйТекстРазобралсяКорректно", "Конструтор текста. Установить текст - Количество строк совпадает с исходным текстом")
			.ДобавитьТест("УвеличитьОтступ", "Конструтор текста. Увеличить отступ")
			.ДобавитьТест("УменьшитьОтступ", "Конструтор текста. Уменьшить отступ")
			.ДобавитьТест("НоваяСтрокаСОтступом", "Конструтор текста. Новая строка с отступом")
			.ДобавитьТест("НоваяСтрокаСНесколькимиОтступами", "Конструтор текста. Новая строка с несколькими уровнями отступов")
			.ДобавитьТест("НоваяСтрокаПоШаблонуСОтступом", "Конструтор текста. Новая строка по шаблону с отступом")
	;
	
КонецПроцедуры

#КонецОбласти

#Область Тесты

Процедура ДобавлениеНовойСтроки() Экспорт
	
	ОжидаемыйРезультат = "1Строка2";
	
	Результат = ЮТРаботаСТекстом.КонструкторТекста()
		.НоваяСтрока("Строка", "1", 2)
		.Текст();
	
	ЮТест.ОжидаетЧто(Результат)
		.Равно(ОжидаемыйРезультат);
	
КонецПроцедуры

Процедура ДобавлениеНовойСтрокиПоШаблону(ПараметрШаблона1, ПараметрШаблона2) Экспорт
	
	Шаблон = ".Установить(""%1"", %2)";
	ОжидаемыйРезультат = СтрШаблон(Шаблон, ПараметрШаблона1, ПараметрШаблона2);
	
	Результат = ЮТРаботаСТекстом.КонструкторТекста()
		.НоваяСтрокаПоШаблону(Шаблон, ПараметрШаблона1, ПараметрШаблона2)
		.Текст();
	
	ЮТест.ОжидаетЧто(Результат)
		.Равно(ОжидаемыйРезультат);
	
КонецПроцедуры

Процедура ДобавлениеТекста() Экспорт
	
	Текст = "Макет = ""|Колонка1|Колонка2|Ы||1  |     2|"";";
	ОжидаемыйРезультат =
		"// Первая строка
		|	Макет = ""|Колонка1|Колонка2|
		|	||1  |     2|"";";
	
	Результат = ЮТРаботаСТекстом
		.КонструкторТекста()
		.НоваяСтрока("// Первая строка")
		.ДобавитьТекст(Текст, 1, "Ы")
		.Текст();
	
	ЮТест.ОжидаетЧто(Результат)
		.Равно(ОжидаемыйРезультат);
	
КонецПроцедуры

Процедура ВставитьПервуюСтроку() Экспорт

	ОжидаемыйРезультат =
		"0Строка3
		|Строка1
		|Строка2";
	
	Результат = ЮТРаботаСТекстом.КонструкторТекста()
		.НоваяСтрока("Строка1")
		.НоваяСтрока("Строка2")
		.ВставитьПервуюСтроку("Строка", "0", 3)
		.Текст();
	
	ЮТест.ОжидаетЧто(Результат)
		.Равно(ОжидаемыйРезультат);
	
КонецПроцедуры

Процедура ВставитьСтроку() Экспорт

	ОжидаемыйРезультат =
		"Строка1
		|0Строка3
		|Строка2";
	
	Результат = ЮТРаботаСТекстом.КонструкторТекста()
		.НоваяСтрока("Строка1")
		.НоваяСтрока("Строка2")
		.ВставитьСтроку(1, "Строка", "0", 3)
		.Текст();
	
	ЮТест.ОжидаетЧто(Результат)
			.Равно(ОжидаемыйРезультат);
	
КонецПроцедуры

Процедура ПереносСтроки() Экспорт
	
	ОжидаемыйРезультат = "Строка1
						|
						|Строка2";
	
	Результат = ЮТРаботаСТекстом.КонструкторТекста()
		.НоваяСтрока("Строка1")
		.ПереносСтроки()
		.НоваяСтрока("Строка2")
		.Текст();
	
	ЮТест.ОжидаетЧто(Результат)
			.Равно(ОжидаемыйРезультат);
	
КонецПроцедуры
		
Процедура Табуляция() Экспорт
	
	ОжидаемыйРезультат =
		"		Строка1
		|Строка2";
	
	Результат = ЮТРаботаСТекстом.КонструкторТекста()
		.НоваяСтрока("Строка1").Табуляция(2)
		.НоваяСтрока("Строка2")
		.Текст();
	
	ЮТест.ОжидаетЧто(Результат)
			.Равно(ОжидаемыйРезультат);
	
КонецПроцедуры
		
Процедура Текст() Экспорт
	
	ОжидаемыйРезультат =
		"		Строка1
		|Строка2
		|  Строка3
		|Строка4";
	
	Результат = ЮТРаботаСТекстом.КонструкторТекста()
		.НоваяСтрока(ОжидаемыйРезультат)
		.Текст();
	
	ЮТест.ОжидаетЧто(Результат)
		.Равно(ОжидаемыйРезультат);
	
КонецПроцедуры

Процедура Разделитель() Экспорт
	
	ОжидаемыйРезультат =
		"Строка1
		|
		|Строка2";
	
	Результат = ЮТРаботаСТекстом.КонструкторТекста()
		.НоваяСтрока("Строка1")
		.НоваяСтрока("Строка2")
		.Разделитель(Символы.ПС + Символы.ПС)
		.Текст();
	
	ЮТест.ОжидаетЧто(Результат)
		.Равно(ОжидаемыйРезультат);
	
КонецПроцедуры
		
Процедура Количество() Экспорт

	ОжидаемыйРезультат = 4;
	
	Результат = ЮТРаботаСТекстом.КонструкторТекста()
		.НоваяСтрока("		Строка1")
		.НоваяСтрока("Строка2")
		.НоваяСтрока("  Строка3")
		.НоваяСтрока("Строка3 ")
		.КоличествоСтрок();
	
	ЮТест.ОжидаетЧто(Результат)
			.Равно(ОжидаемыйРезультат);
	
КонецПроцедуры

Процедура УстановитьТекстИсходныйТекстСоответствует() Экспорт
	
	ОжидаемыйРезультат = "Строка1
						|Строка2
						|Строка3
						|Строка4";
	
	Результат = ЮТРаботаСТекстом.КонструкторТекста()
		.УстановитьТекст(ОжидаемыйРезультат)
		.Текст();
	
	ЮТест.ОжидаетЧто(Результат)
			.Равно(ОжидаемыйРезультат);
	
КонецПроцедуры

Процедура УстановитьТекстИсходныйТекстРазобралсяКорректно() Экспорт
	
	Текст = "Строка1
			|
			|Строка2
			|Строка3
			|Строка4";
	
	ОжидаемыйРезультат = 5;
	
	Результат = ЮТРаботаСТекстом.КонструкторТекста()
		.УстановитьТекст(Текст)
		.КоличествоСтрок();
	
	ЮТест.ОжидаетЧто(Результат)
			.Равно(ОжидаемыйРезультат);
	
КонецПроцедуры

Процедура УвеличитьОтступ() Экспорт
	
	ОжидаемыйРезультат =
		"	Строка1
		|	Строка2";
	
	Результат = ЮТРаботаСТекстом.КонструкторТекста()
		.УвеличитьОтступ()
		.НоваяСтрока("Строка1")
		.НоваяСтрока("Строка2")
		.Текст();
	
	ЮТест.ОжидаетЧто(Результат)
			.Равно(ОжидаемыйРезультат);
	
КонецПроцедуры

Процедура УменьшитьОтступ() Экспорт
	
	ОжидаемыйРезультат =
		"	Строка1
		|Строка2";
	
	Результат = ЮТРаботаСТекстом.КонструкторТекста()
		.УвеличитьОтступ()
		.НоваяСтрока("Строка1")
		.УменьшитьОтступ()
		.НоваяСтрока("Строка2")
		.Текст();
	
	ЮТест.ОжидаетЧто(Результат)
		.Равно(ОжидаемыйРезультат);
	
КонецПроцедуры

Процедура НоваяСтрокаСОтступом() Экспорт
	
	ОжидаемыйРезультат =
		"Строка1
		|	Строка2
		|Строка3";
	
	Результат = ЮТРаботаСТекстом.КонструкторТекста()
		.НоваяСтрока("Строка1")
		.УвеличитьОтступ()
		.НоваяСтрока("Строка2")
		.УменьшитьОтступ()
		.НоваяСтрока("Строка3")
		.Текст();
	
	ЮТест.ОжидаетЧто(Результат)
		.Равно(ОжидаемыйРезультат);
	
КонецПроцедуры

Процедура НоваяСтрокаСНесколькимиОтступами() Экспорт
	
	ОжидаемыйРезультат =
		"Строка1
		|	Строка2
		|		Строка3
		|	Строка4
		|Строка5";
	
	Результат = ЮТРаботаСТекстом.КонструкторТекста()
		.НоваяСтрока("Строка1")
		.УвеличитьОтступ()
		.НоваяСтрока("Строка2")
		.УвеличитьОтступ()
		.НоваяСтрока("Строка3")
		.УменьшитьОтступ()
		.НоваяСтрока("Строка4")
		.УменьшитьОтступ()
		.НоваяСтрока("Строка5")
		.Текст();
	
	ЮТест.ОжидаетЧто(Результат)
		.Равно(ОжидаемыйРезультат);
	
КонецПроцедуры

Процедура НоваяСтрокаПоШаблонуСОтступом() Экспорт
	
	Шаблон = ".Установить(""%1"", %2)";
	Параметр1 = "Сумма";
	Параметр2 = "1000";
	
	ОжидаемыйРезультат = Символы.Таб + СтрШаблон(Шаблон, Параметр1, Параметр2);
	
	Результат = ЮТРаботаСТекстом.КонструкторТекста()
		.УвеличитьОтступ()
		.НоваяСтрокаПоШаблону(Шаблон, Параметр1, Параметр2)
		.Текст();
	
	ЮТест.ОжидаетЧто(Результат)
		.Равно(ОжидаемыйРезультат);
	
КонецПроцедуры

#КонецОбласти
